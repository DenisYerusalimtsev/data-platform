plugins {
    id 'scala'
    id 'java-library'
}

group 'com.prism.dataplatform'
version '1.0.0'

ext {
    scalaVersion = '2.12.14'
    scalaCompatVersion = '2.12'
    sparkVersion = '3.1.0'
    flinkVersion = '1.13.2'
    flinkCompatVersion = '1.13'
    http4sVersion = '1.0.0-M23'
    catsVersion = '2.6.1'
    circeVersion = '0.14.1'

    scalaDependencies = [
            lib    : "org.scala-lang:scala-library:${scalaVersion}",
            logging: "com.typesafe.scala-logging:scala-logging_${scalaCompatVersion}:3.9.2",
            log4j  : "org.apache.logging.log4j:log4j-slf4j-impl:2.11.0",
            scopt  : "com.github.scopt:scopt_${scalaCompatVersion}:4.0.0",
            yaml   : "org.yaml:snakeyaml:1.27"
    ]
    spark = [
            core : "org.apache.spark:spark-core_${scalaCompatVersion}:${sparkVersion}",
            sql  : "org.apache.spark:spark-sql_${scalaCompatVersion}:${sparkVersion}",
            kafka: "org.apache.spark:spark-sql-kafka-0-10_${scalaCompatVersion}:${sparkVersion}",
    ]
    flink = [
            clients  : "org.apache.flink:flink-clients_${scalaCompatVersion}:${flinkVersion}",
            streaming: "org.apache.flink:flink-streaming-scala_${scalaCompatVersion}:${flinkVersion}",
            scala    : "org.apache.flink:flink-scala_${scalaCompatVersion}:${flinkVersion}",
            twitter  : "org.apache.flink:flink-connector-twitter_${scalaCompatVersion}:${flinkVersion}",
            avro     : "org.apache.flink:flink-avro:${flinkVersion}"

    ]
    http = [
            client     : "org.http4s:http4s-client_${scalaCompatVersion}:${http4sVersion}",
            dsl        : "org.http4s:http4s-dsl_${scalaCompatVersion}:${http4sVersion}",
            blazeClient: "org.http4s:http4s-blaze-client_${scalaCompatVersion}:${http4sVersion}",
            http4sCirce: "org.http4s:http4s-circe_${scalaCompatVersion}:${http4sVersion}"
    ]
    circe = [
            generic: "io.circe:circe-generic_${scalaCompatVersion}:${circeVersion}",
            parser : "io.circe:circe-parser_${scalaCompatVersion}:${circeVersion}"
    ]
    cats = [
            core  : "org.typelevel:cats-core_${scalaCompatVersion}:${catsVersion}",
            effect: "org.typelevel:cats-effect_${scalaCompatVersion}:3.2.9"
    ]
    fs2 = [
            core: "co.fs2:fs2-core_${scalaCompatVersion}:3.0.4"
    ]
    json4s = "org.json4s:json4s-native_${scalaCompatVersion}:4.0.3"

    azure = [
            core         : "com.azure:azure-core:1.21.0",
            textanalytics: "com.azure:azure-ai-textanalytics:5.1.3"
    ]
}

allprojects {
    apply plugin: 'scala'
    apply plugin: 'java-library'
    apply plugin: 'application'

    applicationDefaultJvmArgs = ['-Xss256m']

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Xss256m']
    }

    tasks.withType(ScalaCompile) {
        options.compilerArgs += ['-Xss256m']
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation(
                scalaDependencies.lib,
                scalaDependencies.logging,
                scalaDependencies.log4j,
                scalaDependencies.yaml,
                scalaDependencies.scopt
        )

        testImplementation(
                "junit:junit:4.12",
                "org.scalatestplus:scalatestplus-junit_${scalaCompatVersion}:1.0.0-M2",
                "org.mockito:mockito-scala_${scalaCompatVersion}:1.16.25",
                "com.holdenkarau:spark-testing-base_${scalaCompatVersion}:3.0.1_1.0.0",
                "org.apache.flink:flink-test-utils_${scalaCompatVersion}:1.12.2",
                "org.apache.flink:flink-runtime_2.11:1.12.2"
        )

        testImplementation("org.scalatest:scalatest_${scalaCompatVersion}:3.2.9") {
            force(true)
        }
    }
}